PROJECT(LBCpp)
cmake_minimum_required(VERSION 2.6)

IF (CMAKE_SYSTEM_NAME STREQUAL "Linux" AND CMAKE_COMPILER_IS_GNUCC)
SET (CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -fPIC -std=c++0x")
SET (CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -fPIC -D_DEBUG -std=c++0x")
SET (CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -fPIC")
SET (CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -fPIC -D_DEBUG")
ENDIF (CMAKE_SYSTEM_NAME STREQUAL "Linux" AND CMAKE_COMPILER_IS_GNUCC)

IF   (WIN32)
  IF (CMAKE_CL_64)
    SET(LBCPP_BIN_DIRECTORY ${LBCpp_SOURCE_DIR}/bin/win64)
  ELSE (CMAKE_CL_64)
    SET(LBCPP_BIN_DIRECTORY ${LBCpp_SOURCE_DIR}/bin/win32)
  ENDIF (CMAKE_CL_64)
ELSE (WIN32)
  IF   (UNIX)
    IF   (APPLE)
      SET(LBCPP_BIN_DIRECTORY ${LBCpp_SOURCE_DIR}/bin/macosx)
    ELSE (APPLE)
      SET(LBCPP_BIN_DIRECTORY ${LBCpp_SOURCE_DIR}/bin/linux/${CMAKE_BUILD_TYPE})
    ENDIF(APPLE)
  ELSE  (UNIX)
    MESSAGE(FATAL_ERROR "Unsupported platform.")
  ENDIF (UNIX)
ENDIF(WIN32)

SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${LBCPP_BIN_DIRECTORY})
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${LBCPP_BIN_DIRECTORY})

###
### C++ Introspection support
###
ADD_SUBDIRECTORY(introgen)

# generate a .cpp file from an XML introspection file
# ex: GENERATE_INTROSPECTION_CODE(in.xml out.cpp) 
MACRO (GENERATE_INTROSPECTION_CODE INPUT OUTPUT)
  GET_TARGET_PROPERTY(INTROSPECTION_GENERATOR_COMMAND IntrospectionGenerator LOCATION)
  ADD_CUSTOM_COMMAND(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${OUTPUT}
    COMMAND ${INTROSPECTION_GENERATOR_COMMAND} ${CMAKE_CURRENT_SOURCE_DIR}/${INPUT} ${CMAKE_CURRENT_BINARY_DIR}/${OUTPUT} ${CMAKE_CURRENT_SOURCE_DIR}
    MAIN_DEPENDENCY ${INPUT}
    DEPENDS ${INPUT} ${INTROSPECTION_GENERATOR_COMMAND}
    COMMENT "Generating C++ Introspection code ${OUTPUT}."
  )
ENDMACRO(GENERATE_INTROSPECTION_CODE)


MACRO(SOURCE_SUB_GROUP ROOT_NAME SUB_NAME) # other args
  IF (WIN32)
    SOURCE_GROUP(${ROOT_NAME}\\${SUB_NAME} ${ARGN})
  ELSE (WIN32)
    SOURCE_GROUP(${ROOT_NAME}/${SUB_NAME} ${ARGN})
  ENDIF (WIN32)
ENDMACRO(SOURCE_SUB_GROUP)


MACRO(SOURCE_SUB_GROUP2 ROOT_NAME SUB_NAME SUB_NAME2) # other args
  IF (WIN32)
    SOURCE_GROUP(${ROOT_NAME}\\${SUB_NAME}\\${SUB_NAME2} ${ARGN})
  ELSE (WIN32)
    SOURCE_GROUP(${ROOT_NAME}/${SUB_NAME}/${SUB_NAME2} ${ARGN})
  ENDIF (WIN32)
ENDMACRO(SOURCE_SUB_GROUP2)

ADD_SUBDIRECTORY(juce)
ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(projects)

OPTION(LBCPP_EXPLORER "Include explorer into the project" ON)
IF (LBCPP_EXPLORER)
  ADD_SUBDIRECTORY(explorer)
ENDIF(LBCPP_EXPLORER)

OPTION(LBCPP_DOCUMENTATION "Include documentation into the project" OFF)
IF (LBCPP_DOCUMENTATION)
  ADD_SUBDIRECTORY(dox)
ENDIF (LBCPP_DOCUMENTATION)
