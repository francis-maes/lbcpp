PROJECT(CRAlgo)
cmake_minimum_required(VERSION 2.6)

ADD_SUBDIRECTORY(synopsis)
ADD_SUBDIRECTORY(compiler)
ADD_SUBDIRECTORY(src)

###
### CR-compilator CMake support
###
# Where is CR-algo compiler?
GET_TARGET_PROPERTY(CRALGO_COMPILER_COMMAND CRAlgoCompiler LOCATION)

# add_cralgorithm macro
MACRO(ADD_CRALGORITHM OUTPUTFILE INPUTFILE)
 # MESSAGE("${CRALGO_COMPILER_COMMAND} ${INPUTFILE} ${OUTPUTFILE}")
  ADD_CUSTOM_COMMAND(OUTPUT ${OUTPUTFILE}
                  COMMAND ${CRALGO_COMPILER_COMMAND} ${INPUTFILE} ${OUTPUTFILE}
                  MAIN_DEPENDENCY ${INPUTFILE}
                  WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
                  DEPENDS ${CRALGO_COMPILER_COMMAND} ${INPUTFILE}
 		  COMMENT "CR-algorithm ${INPUTFILE}"
  )
ENDMACRO(ADD_CRALGORITHM)

MACRO(ADD_CRALGORITHMS) #SOURCES
  SET(CRALGORITHMS_OUTPUT_FILES "")
  foreach(SOURCE ${ARGN})
     SET(inputfile ${CMAKE_CURRENT_SOURCE_DIR}/${SOURCE})
     SET(outputfile ${CMAKE_CURRENT_BINARY_DIR}/${SOURCE})
     STRING(REPLACE ".cr++" ".cpp" outputfile ${outputfile})
   #  MESSAGE("output: " ${outputfile})
     ADD_CRALGORITHM(${outputfile} ${inputfile})
     SET(CRALGORITHMS_OUTPUT_FILES ${CRALGORITHMS_OUTPUT_FILES} "${outputfile}")
  endforeach(SOURCE)
ENDMACRO(ADD_CRALGORITHMS)


# add_crexecutable macro
MACRO(ADD_CREXECUTABLE NAME) #SOURCES
  ADD_CRALGORITHMS(${ARGN})
  set_source_files_properties(${ARGN} PROPERTIES HEADER_FILE_ONLY TRUE)
  SOURCE_GROUP(GeneratedCode FILES ${CRALGORITHMS_OUTPUT_FILES})
  ADD_EXECUTABLE(${NAME} ${CRALGORITHMS_OUTPUT_FILES} ${ARGN})
  TARGET_LINK_LIBRARIES(${NAME} cralgo-cpp)
ENDMACRO(ADD_CREXECUTABLE)

# add_crtest macro
MACRO(ADD_CRTEST SOURCEFILE)
  STRING(REPLACE ".cr++" "" targetname ${SOURCEFILE})
  SET(targetname "test-${targetname}")
  ADD_CREXECUTABLE(${targetname} ${SOURCEFILE})
ENDMACRO(ADD_CRTEST)

## Tests
ADD_SUBDIRECTORY(test)

